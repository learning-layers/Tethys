//
// @author Gordon Lawrenz <lawrenz@dbis.rwth-aachen.de>
//
// use gradle.properties to define parameters
// see gradle.properties.example for help
//
apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'eclipse'
apply plugin: 'cargo-base'

import org.gradle.api.plugins.cargo.convention.Deployable
import org.gradle.api.plugins.cargo.tasks.remote.CargoDeployRemote

task wrapper(type: Wrapper) {
  gradleVersion = '2.1'
}

sourceCompatibility = 1.7

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.gradle.api.plugins:gradle-cargo-plugin:1.5.1'
    }
}

configurations {
    extraStuff
}

repositories {
    mavenCentral()
    flatDir { dirs "lib" }
}

//All needed dependencies, splitted in two groups to reduce size of the created .war
dependencies {
	testCompile group: 'junit', name: 'junit', version: '4.+'

	providedCompile 'org.apache.tomcat:tomcat-servlet-api:7.0.37'
    
    runtime ('com.wordnik:swagger-annotations:1.3.2') { transitive = false }
    runtime ('com.wordnik:swagger-core_2.10:1.3.2') { transitive = false }
    runtime ('com.wordnik:swagger-jaxrs_2.10:1.3.2') { transitive = false }
    runtime ('com.wordnik:swagger-jersey-jaxrs_2.10:1.3.2') { transitive = false }  
    
    extraStuff ('com.wordnik:swagger-annotations:1.3.2') 
    extraStuff  ('com.wordnik:swagger-core_2.10:1.3.2')
    extraStuff  ('com.wordnik:swagger-jaxrs_2.10:1.3.2') 
    extraStuff  ('com.wordnik:swagger-jersey-jaxrs_2.10:1.3.2')
    
    providedCompile 'com.google.code.gson:gson:2.2.4'
    providedCompile 'javax.persistence:persistence-api:1.0'
    providedCompile 'com.sun.jersey:jersey-json:1.13'
    providedCompile 'mysql:mysql-connector-java:5.1.26'
    providedCompile "javax.ws.rs:jsr311-api:1.1.1"
    providedCompile 'com.sun.jersey:jersey-server:1.13'
    providedCompile 'com.sun.jersey:jersey-core:1.13'
    providedCompile 'com.sun.jersey:jersey-servlet:1.13'

}

test {
    systemProperties 'property': 'value'
}

uploadArchives {
    repositories {
       flatDir {
           dirs 'repos'
       }
    }
}

//Include extraStuff for compilation
sourceSets.main.compileClasspath += [configurations.extraStuff]

// optional: if using 'eclipse' plugin
eclipse {
  classpath {
    plusConfigurations += [configurations.extraStuff]
  }
}

//Environment class, provides all necessary information of the server for the cargo plugin
class RemoteEnvironment {
	String containerId
	String hostname
	Integer port
	String username
	String password
}

//Container class, provides all necessary of the be deployed container for the cargo plugin
class Container {
    String name
    @Nested
    RemoteEnvironment remoteEnvironment
    String context
}

//List of remote action types
def remoteTypes = ['deploy','redeploy','undeploy']

//Map of all remote server environments
def remoteEnvironments = [
	'TethysServer': new RemoteEnvironment(
		containerId:	cargo_containerId,
		hostname:		cargo_hostname, 
		port:			cargo_port as Integer,
		username:		cargo_username,
		password:		cargo_password
	)
]

//List of all Containers
def containers = [
	new Container(
		name:				cargo_deployName,	
		context:			cargo_deployName,
		remoteEnvironment:	remoteEnvironments.get('TethysServer')
	),
	new Container(
		name:				cargo_developName,
		context:			cargo_developName+cargo_deployName,
		remoteEnvironment:	remoteEnvironments.get('TethysServer')
	),
	new Container(
		name:				cargo_testName,
		context:			cargo_testName+cargo_deployName,
		remoteEnvironment:	remoteEnvironments.get('TethysServer')
	)
]		

//Provides all Tasks
remoteTypes.each { type ->
	containers.each { config ->
		//Defines Cargo tasks for specified remote action types and containers 
		task "remote${type.capitalize()}${config.name.capitalize()}" (type: Class.forName("org.gradle.api.plugins.cargo.tasks.remote.Cargo${type.capitalize()}Remote")) {
			containerId	= config.remoteEnvironment.containerId
			port		= config.remoteEnvironment.port
   			deployables	= [new Deployable(file: file(war.archivePath), context: config.context)]
	   		hostname	= config.remoteEnvironment.hostname
   			username	= config.remoteEnvironment.username
   			password	= config.remoteEnvironment.password
   			
   			//Print the used settings before executing the cargo task
   			doFirst{
   				println "-------------------------------------"
   				println "Cargo Settings:"
   				println "Target: http://${config.remoteEnvironment.hostname}:${config.remoteEnvironment.port}/${config.context}"
   				println "User credentials: ${config.remoteEnvironment.username}:${config.remoteEnvironment.password}"
   				println "-------------------------------------"
   			}
   			//Manipulate the web.xml so that swagger knows our api.path
   			doFirst{
   				war{
   					eachFile{
						if (it.name == 'web.xml'){
							it.filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: ['Tethys': config.context])
							println "BLA: ${config.context}"
						}
					}
				}
				compileJava.execute()
				processResources.execute()
				classes.execute()
				war.execute()
				assemble.execute()
				build.execute()
   			}
		}
	}
}
